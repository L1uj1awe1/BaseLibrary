if (isBuildLibraryNetwork) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
    apply plugin: 'com.novoda.bintray-release'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"


    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    resourcePrefix "network_"

    sourceSets {
        main {
            if (isBuildLibraryNetwork) {
                manifest.srcFile 'src/main/manifest/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/release/AndroidManifest.xml'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    if (!isBuildLibraryNetwork) {

        //生成源文件
        task sourcesJar(type: Jar) {
            from android.sourceSets.main.java.srcDirs
            classifier = 'sources'
        }

        //生成Javadoc文档
        task javadoc(type: Javadoc) {
            source = android.sourceSets.main.java.srcDirs
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        }

        //文档打包成jar
        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        //拷贝javadoc文件
        task copyDoc(type: Copy) {
            from "${buildDir}/docs/"
            into "docs"
        }

        //上传到JCenter所需要的源码文件
        artifacts {
            archives javadocJar
            archives sourcesJar
        }

        //解决 JavaDoc 中文注释生成失败的问题
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
            options.addStringOption('encoding', 'UTF-8')
            options.addStringOption('charSet', 'UTF-8')

            excludes = ['**/*.kt']
        }

//        tasks.withType(Javadoc).all {
//            enabled = false
//        }

        //发布到 Bintray
        publish {
            userOrg = 'f1ght1n9code' // 用户名，小写
            groupId = 'com.readboy.baselibrary' // 以后访问 JCenter上此项目的路径，一般和库项目的包名一致
            artifactId = 'network' // Bintray.com 创建的 Package 名
            publishVersion = '1.0.6' // 版本号
            desc = '引入 Retrofit 2.0 依赖和 Demo' //版本说明，随意
            website = 'https://github.com/L1uj1awe1/BaseLibrary.git' //关于这个开源项目的网站，随意

            // implementation 'com.readboy.baselibrary:network:1.0.6'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // test
    testImplementation 'junit:junit:4.12'

    // Android Library Support
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Other Utils implementation
    implementation 'com.blankj:utilcode:1.20.1' // Android Util Library

    // Web Socket
    api "org.java-websocket:Java-WebSocket:1.3.8"
    api "com.google.code.gson:gson:2.8.5"

    // OkHttp
    api 'com.squareup.okhttp3:okhttp:3.10.0'
    api "com.squareup.okhttp3:logging-interceptor:3.6.0" // OkHttp 日志拦截

    // Retrofit 2.0
    api 'com.squareup.retrofit2:retrofit:2.3.0'
    api "com.squareup.retrofit2:converter-gson:2.3.0" // GsonConverterFactory
    api "com.squareup.retrofit2:adapter-rxjava2:2.3.0" // RxJava2CallAdapterFactory
    api "io.reactivex.rxjava2:rxandroid:2.0.1" // AndroidSchedulers.mainThread()

}
