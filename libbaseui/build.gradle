if (isBuildLibraryUI) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
    apply plugin: 'com.novoda.bintray-release'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"


    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    resourcePrefix "ui_"

    sourceSets {
        main {
            if (isBuildLibraryUI) {
                manifest.srcFile 'src/main/manifest/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/release/AndroidManifest.xml'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    if (!isBuildLibraryUI) {

        //生成源文件
        task sourcesJar(type: Jar) {
            from android.sourceSets.main.java.srcDirs
            classifier = 'sources'
        }

        //生成Javadoc文档
        task javadoc(type: Javadoc) {
            source = android.sourceSets.main.java.srcDirs
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        }

        //文档打包成jar
        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        //拷贝javadoc文件
        task copyDoc(type: Copy) {
            from "${buildDir}/docs/"
            into "docs"
        }

        //上传到JCenter所需要的源码文件
        artifacts {
            archives javadocJar
            archives sourcesJar
        }

        //解决 JavaDoc 中文注释生成失败的问题
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
            options.addStringOption('encoding', 'UTF-8')
            options.addStringOption('charSet', 'UTF-8')

            excludes = ['**/*.kt']
        }

//        tasks.withType(Javadoc).all {
//            enabled = false
//        }

        //发布到 Bintray
        publish {
            userOrg = 'f1ght1n9' //organization id 企业名称或ID
            groupId = 'com.readboy.baselibrary' //以后访问 jcenter上此项目的路径，一般和库项目的包名一致
            artifactId = 'ui' //bintray.com 创建的 Package 名
            publishVersion = '1.0.0' //版本号
            desc = '任何关于UI的基础库' //版本说明，随意
            website = 'https://github.com/L1uj1awe1/BaseLibrary.git' //关于这个开源项目的网站，随意
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.android.support:appcompat-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation 'com.jude:swipebackhelper:3.1.2' // Swipe Back Activity - https://github.com/Jude95/SwipeBackHelper
    implementation 'com.blankj:utilcode:1.19.3' // Android Util Library - https://github.com/Blankj/AndroidUtilCode

    api 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-14' // 下拉刷新 - https://github.com/scwang90/SmartRefreshLayout
    api 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-alpha-14' // 下拉刷新header，使用默认header可以不要这行
    api "com.android.support:recyclerview-v7:27.1.1" // RecyclerView
    api 'com.youth.banner:banner:1.4.10'  // banner - https://github.com/youth5201314/banner
}
